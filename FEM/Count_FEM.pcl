#include "appforms.p"
#include	<lpenums.i>

CLASS COUNT_FEM

/* Classwide Variables */
   CLASSWIDE WIDGET form_id, selfrm_01, seldbox_Elements
   CLASSWIDE WIDGET sep_01, btnApply, btnCancel

   FUNCTION init()
      REAL yloc_main
      REAL yloc_selfrm_01=0.

      form_id=ui_form_create(                     @
      /* callback */         "",                  @
      /*     x    */         FORM_X_LOC_SML,      @
      /*     y    */         FORM_Y_LOC,          @
      /* position */         "UL",                @
      /*   width  */         FORM_WID_SML,        @
      /*  height  */         FORM_HGT_TALL,       @
      /*   label  */         "Element Renumber",  @
      /*  unused  */         "")

      yloc_main = FORM_T_MARGIN

      selfrm_01=ui_selectframe_create(                    @
      /*  parent  */                  form_id,            @
      /* callback */                  "",                 @
      /*     x    */                  FORM_L_MARGIN,      @
      /*     y    */                  yloc_main,          @
      /*   width  */                  SFRAME_WID_SINGLE,  @
      /*  height  */                  1.0,                @
      /*   label  */                  "",                 @
      /*  recycle */                  FALSE)

      yloc_selfrm_01 = SFRAME_T_MARGIN

      seldbox_Elements=ui_selectdatabox_create(                   @
      /*    parent    */                       selfrm_01,         @
      /*   callback   */                       "",                @
      /*       x      */                       FORM_L_MARGIN,     @
      /*       y      */                       yloc_selfrm_01,    @
      /* label_length */                       0.0,               @
      /*  box_length  */                       SDBOX_WID_SINGLE,  @
      /*     label    */                       "Select Element",  @
      /*     value    */                       "",                @
      /*  label_above */                       TRUE,             @
      /*   datatype   */                       "ELEMENT",         @
      /*    prompt    */                       "Select Element")

      yloc_selfrm_01 += SDBOX_HGT_LABOVE    + @
                        INTER_WIDGET_SPACE

      ui_wid_set(selfrm_01,"HEIGHT",yloc_selfrm_01)

      yloc_main += SFRAME_2EDGE        + @
                   INTER_WIDGET_SPACE  + @
                   yloc_selfrm_01

      btnApply=ui_button_create(                     @
      /*   parent  */           form_id,             @
      /*  callback */           "cb_Apply",          @
      /*     x     */           BUTTON_HALF_X_LOC1,  @
      /*     y     */           yloc_main,           @
      /*   width   */           BUTTON_WID_HALF,     @
      /*   height  */           BUTTON_DEFAULT_HGT,  @
      /*   label   */           "Apply",             @
      /*    TRUE   */           TRUE,                @
      /* highlight */           TRUE)

      btnCancel=ui_button_create(                     @
      /*   parent  */            form_id,             @
      /*  callback */            "cb_Cancel",         @
      /*     x     */            BUTTON_HALF_X_LOC2,  @
      /*     y     */            yloc_main,           @
      /*   width   */            BUTTON_WID_HALF,     @
      /*   height  */            BUTTON_DEFAULT_HGT,  @
      /*   label   */            "Cancel",            @
      /*    TRUE   */            TRUE,                @
      /* highlight */            FALSE)

      yloc_main += BUTTON_DEFAULT_HGT  + @
                   INTER_WIDGET_SPACE

      ui_wid_set(form_id,"HEIGHT",yloc_main)
      
	END FUNCTION
	
	FUNCTION display()
		ui_form_display("COUNT_FEM")
	END FUNCTION
	
	FUNCTION cb_Apply()
		COUNT_FEM.COUNT()
	END FUNCTION
	
	FUNCTION cb_Cancel()
		ui_form_hide("COUNT_FEM")
	END FUNCTION
	
	FUNCTION COUNT()
		STRING selected_element[virtual]
		INTEGER nElm, status, ia_ElemID(virtual)
				
		ui_wid_get_vstring(seldbox_Elements, "value", selected_element)
		nElm = fem_u_count_id_list(LP_SUBLIST_ELEMENT,selected_element,TRUE, status)
		
		sys_allocate_array(ia_ElemID, 1, nElm)
		
		fem_u_get_id_list(LP_SUBLIST_ELEMENT, selected_element, nElm, TRUE, ia_ElemID)
		
		DUMP "TOTAL ELEMENT NUMBERS = " // str_from_integer(nElm)
		
	    INTEGER idx, nQuad, nTri, nBar, nPnt
	    INTEGER ia_ShapeId(virtual)
	    
	    sys_allocate_array(ia_ShapeId, 1, nElm)
	    
	    db_get_elem_shape_for_id(nElm, ia_ElemID, ia_ShapeId)
	    
		nQuad = 0
		nTri = 0
		nBar = 0
		nPnt = 0
		
	    FOR(idx=1 TO nElm)
			SWITCH(ia_ShapeId(idx))
				CASE(1);
					nPnt += 1
				CASE(2);
					nBar += 1
				CASE(3);
					nTri += 1
				CASE(4);
					nQuad += 1
			END SWITCH
 		END for
		
		dump "Point Element Number = " // str_from_integer(nPnt)
		dump "Bar Element Number = " // str_from_integer(nBar)
		dump "Tri Element Number = " // str_from_integer(nTri)
		dump "Quad Element Number = " // str_from_integer(nQuad)
		
	END FUNCTION
END CLASS